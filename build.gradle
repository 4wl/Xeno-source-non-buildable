buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3.4' // Latest 2 version of ForgeGradle will update to 5.1.24 later. Versions list: https://maven.minecraftforge.net/net/minecraftforge/gradle/ForgeGradle/maven-metadata.xml
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'me.xenodevs'
version = '1.1'
archivesBaseName = 'Xeno'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
    version = project.forgeVersion
    runDir = 'run'
    mappings = project.mcpVersion
    coreMod = 'me.wolfsurge.mixin.MixinLoader'
    makeObfSourceJar = false
}

repositories {
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = 'https://lukflug.github.io/maven' }
    maven { url = 'https://jitpack.io' }

    mavenCentral()
}

dependencies {
    compile('org.spongepowered:mixin:0.7.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core' // we want to exclude this as well because 0.7.11 includes it too new for MC
    }

    compile('club.minnced:java-discord-rpc:2.0.2') {
        exclude module: 'jna'
    }

    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    compile 'com.github.ZeroMemes:Alpine:1.5'
    compile 'net.jodah:typetools:0.5.0'
    compile 'com.mojang.brigadier:brigadier:1.0.18'
    compile group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.23'
    compile group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar.manifest.attributes(
        'MixinConfigs': 'mixins.xeno.json',
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'TweakOrder': 0,
        'FMLCorePluginContainsFMLMod': true,
        'FMLCorePlugin': 'me.wolfsurge.mixin.MixinLoader',
        'ForceLoadAsMod': true,
)

shadowJar {
    dependencies {
        include(dependency('com.github.ZeroMemes:Alpine'))
        include(dependency('net.jodah:typetools'))
        include(dependency('org.spongepowered:mixin'))
        include(dependency('net.sourceforge.htmlunit:htmlunit'))
        include(dependency('club.minnced:java-discord-rpc'))
        include(dependency('com.moandjiezana.toml:toml4j'))
        include(dependency('com.mojang.brigadier:brigadier'))
        include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'release'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.xeno.refmap.json'
}

build.dependsOn(shadowJar)